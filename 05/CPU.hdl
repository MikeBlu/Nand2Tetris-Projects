// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/5/CPU.hdl
/**
 * The Hack Central Processing unit (CPU).
 * Parses the binary code in the instruction input and executes it according to the
 * Hack machine language specification. In the case of a C-instruction, computes the
 * function specified by the instruction. If the instruction specifies to read a memory
 * value, the inM input is expected to contain this value. If the instruction specifies
 * to write a value to the memory, sets the outM output to this value, sets the addressM
 * output to the target address, and asserts the writeM output (when writeM = 0, any
 * value may appear in outM).
 * If the reset input is 0, computes the address of the next instruction and sets the
 * pc output to that value. If the reset input is 1, sets pc to 0.
 * Note: The outM and writeM outputs are combinational: they are affected by the
 * instruction's execution during the current cycle. The addressM and pc outputs are
 * clocked: although they are affected by the instruction's execution, they commit to
 * their new values only in the next cycle.
 */
CHIP CPU {

    IN  inM[16],         // M value input  (M = contents of RAM[A])
        instruction[16], // Instruction for execution
        reset;           // Signals whether to re-start the current
                         // program (reset==1) or continue executing
                         // the current program (reset==0).

    OUT outM[16],        // M value output
        writeM,          // Write to M? 
        addressM[15],    // Address in data memory (of M)
        pc[15];          // address of next instruction

    PARTS:
    // Parse Instruction Type (A or C)
    DMux(in=true,sel=instruction[15],a=aIns,b=cIns);
	// Register D & A
        // DATA REGISTER
        And(a=cIns,b=instruction[4],out=writeD);
        DRegister(in=aluOut, load=writeD, out=outD);
        // ADDRESS REGISTER
        And(a=instruction[5],b=cIns,out=writeA); // determine if Address Register is set this cycle
        Or(a=writeA, b=aIns, out=setA);
        Mux16(a=aluOut, b=instruction, sel=aIns, out=inA); // determine if AR set by A-instruction or computation result
        Register(in=inA, load=setA, out[0..14]=addressM, out=outA);

    // Determine if M memory should be written to
    And(a=cIns,b=instruction[3],out=writeM);
    
    // Program Counter
        // check for jump condition & fulfilment
        Not(in=aluZr, out=nE); // not-equal
        Not(in=aluNg, out=pS); // positive
        And(a=nE, b=pS, out=gT); // greater-than
        Or(a=pS, b=aluZr, out=gE); // greater-than-or-equal
        And(a=aluNg, b=nE, out=lT); // less-than
        Or(a=aluNg, b=aluZr, out=lE); // less-than-or-equal

        Mux8Way16(a[0]=false, b[0]=gT, c[0]=aluZr, d[0]=gE, e[0]=lT, f[0]=nE, g[0]=lE, h=true, sel=instruction[0..2], out[0]=jumpCond);
        And(a=jumpCond, b=cIns, out=jumpOut);
        
    PC(in=outA, load=jumpOut, inc=true, reset=reset, out[0..14]=pc);
    // ALU
    Mux16(a=outA, b=inM, sel=instruction[12], out=ySource);
    ALU(x=outD, y=ySource, zx=instruction[11], nx=instruction[10], zy=instruction[9], ny=instruction[8], f=instruction[7], no=instruction[6], out=aluOut, out=outM, zr=aluZr, ng=aluNg);
}